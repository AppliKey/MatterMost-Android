plugins {
    id "me.tatarka.retrolambda" version "3.2.2"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

android {
    compileOptions.incremental = false

    compileSdkVersion 25
    buildToolsVersion "24.0.3"

    signingConfigs {
        debug {
            storeFile file("keys/debug.keystore")
        }

//        release {
//            storeFile file("other.keystore")
//            storePassword "android"
//            keyAlias "androiddebugkey"
//            keyPassword "android"
//        }
    }

    defaultConfig {
        applicationId "com.applikey.mattermost"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 5
        versionName "0.2"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {

        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFile('proguard-rules.pro')
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
        }

        release {
//            signingConfig signingConfigs.debug
            minifyEnabled true
            proguardFile('proguard-rules.pro')
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }

    productFlavors {
        instrumentation {
            buildConfigField "boolean", "SHOULD_REPLACE_CREDENTIALS", 'false'
            buildConfigField "String", "PRESET_SERVER", '\"\"'
            buildConfigField "String", "USER_NAME", '\"\"'
            buildConfigField "String", "USER_PASSWORD", '\"\"'

        }

        prod {
            buildConfigField "boolean", "SHOULD_REPLACE_CREDENTIALS",
                    project.hasProperty("replacecred") ? "${project.ext.get("replacecred")}" : 'false'
            buildConfigField "String", "PRESET_SERVER",
                    project.hasProperty("server") ? "\"${project.ext.get("server")}\"" : '\"\"'
            buildConfigField "String", "USER_NAME",
                    project.hasProperty("user.name") ? "\"${project.ext.get("user.name")}\"" : '\"\"'
            buildConfigField "String", "USER_PASSWORD",
                    project.hasProperty("user.password") ? "\"${project.ext.get("user.password")}\"" : '\"\"'
        }

    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    kapt {
        generateStubs = true
    }

}

repositories {
    maven { url "https://jitpack.io" }
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'

    compile 'com.jakewharton:butterknife:7.0.1' // views injections

    // dagger 2
    provided 'org.glassfish:javax.annotation:10.0-b28'
    apt 'com.google.dagger:dagger-compiler:2.7'
    compile 'com.google.dagger:dagger:2.7'

    // web
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile "com.squareup.retrofit2:converter-gson:2.1.0"
    compile "com.squareup.retrofit2:adapter-rxjava:2.1.0"
    compile "com.squareup.okhttp3:logging-interceptor:3.4.2"
    compile 'com.squareup.okhttp3:okhttp:3.4.2'

    // rx
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.2.2'
    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'
    compile 'com.github.pwittchen:reactivenetwork:0.2.0'

    // eventbus
    compile 'org.greenrobot:eventbus:3.0.0'

    // image loading and processing
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

    // views
    compile 'com.tasomaniac:delayed-progress:0.3'
    compile('com.mikepenz:materialdrawer:5.6.0@aar') {
        transitive = true
    }

    // mvp
    compile 'com.arello-mobile:moxy:0.5.4'
    compile 'com.arello-mobile:moxy-android:0.5.4'
    apt 'com.arello-mobile:moxy-compiler:0.5.4'
    

    // stetho
    compile 'com.facebook.stetho:stetho:1.4.1'
    compile 'com.uphyca:stetho_realm:2.0.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.1'
    //logging
    compile 'com.jakewharton.timber:timber:4.3.1'

    // stream api
    compile 'com.annimon:stream:1.1.2'

    //Realm Adapter
    compile 'io.realm:android-adapters:1.4.0'

    // crashlytics
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true
    }

    // lint
    compile 'nl.littlerobots.rxlint:rxlint:1.0'

    //leak canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // gcm
    compile 'com.google.android.gms:play-services-gcm:9.8.0'

    // animations
    compile "com.andkulikov:transitionseverywhere:1.6.9"

    //sockets
    compile 'com.neovisionaries:nv-websocket-client:1.30'

    //emoji
    compile 'com.vanniktech:emoji:0.4.0-SNAPSHOT'
    compile 'com.vdurmont:emoji-java:3.1.3'

    //markdown
    compile 'com.github.Commit451:bypasses:1.0.4'

    //rxpaparazzo
    compile "com.github.miguelbcr:RxPaparazzo:0.3.2"

    compile 'com.android.support:multidex:1.0.1'

    //sticky header
    compile ('com.timehop.stickyheadersrecyclerview:library:[0.4.3]@aar') {
        exclude module:'com.android.support:recyclerview-v7:22.2.0'
        exclude module:'com.android.support:support-annotations:22.2.0'
        exclude module:'com.android.support:support-v4:22.2.0'
    }

    compile 'com.android.support:multidex:1.0.1'

    androidTestCompile "org.jetbrains.kotlin:kotlin-stdlib:1.0.5"
}
android.variantFilter { variant ->
    if(variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('instrumentation')) {
        variant.setIgnore(true);
    }
}

